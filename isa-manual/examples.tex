% -*- mode: latex; tab-width: 2; indent-tabs-mode: nil; -*-
%------------------------------------------------------------------------------
% MRISC32 ISA Manual - Examples.
%
% This work is licensed under the Creative Commons Attribution-ShareAlike 4.0
% International License. To view a copy of this license, visit
% http://creativecommons.org/licenses/by-sa/4.0/ or send a letter to
% Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
%------------------------------------------------------------------------------

% We do assembler examples in single-column mode to better fit the code.
\onecolumn

\chapter{Examples}

This is a non-normative section that contains programs that exemplify various
aspects of the MRISC32 instruction set architecture.

\section{Basic operations}

\subsection{Push/pop stack}

\begin{lstlisting}[style=assembler]
non_leaf_function:
    ; Push registers r16, r17 and lr onto the stack
    add   sp, sp, #-12
    stw   r16, [sp, #0]
    stw   r17, [sp, #4]
    stw   lr, [sp, #8]

    ; ...

    ; Pop registers and return from function
    ldw   lr, [sp, #8]
    ldw   r17, [sp, #4]
    ldw   r16, [sp, #0]
    add   sp, sp, #12
    ret                     ; Alias for j lr, #0
\end{lstlisting}

\subsection{Simple loop}

\begin{lstlisting}[style=assembler]
    ldi   r1, #loop_count   ; r1 holds the loop counter
loop:
    ; ...
    add   r1, r1, #-1       ; Decrement the loop counter
    bnz   r1, loop          ; Branch if r1 != 0
\end{lstlisting}

\subsection{Conditional selection}

\begin{lstlisting}[style=assembler]
    sne   r4, r1, #42
    sel   r4, r2, r3        ; r4 = (r1 != 42) ? r2 : r3
\end{lstlisting}

\section{Vector operation}

\subsection{saxpy}

Several BLAS routines, including saxpy, are easily vectorized for the MRISC32
instruction set.

\begin{lstlisting}[style=assembler]
; void saxpy(size_t n, const float a, const float *x, float *y)
; {
;   for (size_t i = 0; i < n; i++)
;     y[i] = a * x[i] + y[i];
; }
;
; Register arguments:
;   r1 - n
;   r2 - a
;   r3 - x
;   r4 - y

saxpy:
    bz    r1, 2f          ; Nothing to do?
    cpuid vl, z, z        ; Query the maximum vector length
1:
    minu  vl, vl, r1      ; Define the operation vector length
    sub   r1, r1, vl      ; Decrement loop counter
    ldw   v1, [r3, #4]    ; Load x (element stride = 4 bytes)
    ldw   v2, [r4, #4]    ; Load y
    fmul  v1, v1, r2      ; x * a
    fadd  v1, v1, v2      ; + y
    stw   v1, [r4, #4]    ; Store y
    ldea  r3, [r3, vl*4]  ; Increment address (x)
    ldea  r4, [r4, vl*4]  ; Increment address (y)
    bnz   r1, 1b
2:
    ret
\end{lstlisting}

\subsection{Linear interpolation}

Linear interpolation can be implemented using vector gather load. Here is an
example of one-dimensional floating-point interpolation.

\begin{lstlisting}[style=assembler]
; void lerp(size_t n, const float t0, const float dt, const float *x, float *y)
; {
;   float t = t0;
;   for (size_t i = 0; i < n; i++)
;   {
;     int k = (int)t;
;     float w = t - (float)k;
;     y[i] = x[k] + w * (x[k+1] - x[k]);
;     t += dt;
;   }
; }
;
; Register arguments:
;   r1 - n
;   r2 - t0
;   r3 - dt
;   r4 - x
;   r5 - y

lerp:
    bz    r1, 2f          ; Nothing to do?

    cpuid vl, z, z        ; Query maximum vector length

    add   r6, r4, #4      ; r6 = &x[1]
    itof  r7, vl, z

    ldea  v1, [z, #1]     ; v1 = [0, 1, 2, ...]
    itof  v1, v1, z
    fmul  v1, v1, r3      ; v1 = dt * [0.0, 1.0, 2.0, ...]

    fmul  r7, r3, r7      ; r7 = dt * maximum vector length
1:
    minu  vl, vl, r1      ; Define the operation vector length
    sub   r1, r1, vl      ; Decrement loop counter

    ftoi  v2, v1, z       ; v2 = integer indexes (k)
    itof  v3, v2, z
    fsub  v3, v1, v3      ; v3 = interpolation weight (w)

    ldw   v4, [r4, v2*4]  ; Load x[k]
    ldw   v5, [r6, v2*4]  ; Load x[k+1]

    fsub  v5, v5, v4
    fmul  v5, v5, v3
    fadd  v5, v4, v5      ; v5 = x[k] + w * (x[k+1] - x[k])

    stw   v5, [r5, #4]    ; Store y (element stride = 4 bytes)

    ldea  r5, [r5, vl*4]  ; Increment address (y)
    fadd  v1, v1, r7      ; Increment t
    bnz   r1, 1b
2:
    ret
\end{lstlisting}

\subsection{Reverse bytes}

Reversing a byte array (e.g. for horizontal mirroring of an image) can be
achieved by copying 32-bit words in reverse order (using a negative stride when
storing the words), in combination with reversing the bytes of each individual
word using the SHUF instruction.

\begin{lstlisting}[style=assembler]
; void revbytes(size_t n, const uint8_t *x, uint8_t *y)
; {
;   for (size_t i = 0; i < n; i++)
;     y[n-1-i] = x[i];
; }
;
; Register arguments:
;   r1 - n
;   r2 - x
;   r3 - y
;
; Assumptions:
;   n is a multiple of 4

revbytes:
    bz    r1, 2f          ; Nothing to do?
    add   r4, r1, #-4
    add   r3, r3, r4      ; r3 = &y[n-4]
    lsr   r1, r1, #2      ; r1 = number of words
    cpuid vl, z, z        ; Query the maximum vector length
    lsl   r4, vl, #2      ; r4 = 4 * max vector length
1:
    minu  vl, vl, r1      ; Define the operation vector length
    sub   r1, r1, vl      ; Decrement loop counter
    ldw   v1, [r2, #4]    ; Load x (element stride = 4 bytes)
    shuf  v1, v1, #0b000001010011  ; Reverse bytes of each word
    stw   v1, [r3, #-4]   ; Store y (element stride = -4 bytes)
    add   r2, r2, r4      ; Increment address (x)
    sub   r3, r3, r4      ; Decrement address (y)
    bnz   r1, 1b
2:
    ret
\end{lstlisting}

\twocolumn
